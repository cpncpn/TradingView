// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © tanayroy

//@version=4
study("ICHIMOKU Trading beta",shorttitle="ICHIMOKU",overlay=true)
tenkan_sen_period = input(9, minval=1, title="Tenkan Sen Length")
kijun_sen_period = input(26, minval=1, title="Kijun Sen Length")
senkou_b_len = input(52, minval=1, title="Senkou B Length")
displacement = input(26, minval=1, title="Displacement")
chikouspan_consolidation_bar=input(5, minval=1, title="Chikou Consolidation Bar")
show_comment=input(false,'Show Comment',type=input.bool)
kumo_shadow_period=input(252, minval=1, title="Kumo Shadow Analyzing Period")
donchian(len) => avg(lowest(len), highest(len))
//tanken sen is average of highest high and lowest low for last 9 days.
//Its the short term average to indicate price movement
tenkan_sen=donchian(tenkan_sen_period)
plot(tenkan_sen, color=color.red)

kijun_sen=donchian(kijun_sen_period )
plot(kijun_sen,color=color.green)
//plotting chikou sen
plot(close,offset=-displacement,color=color.purple)

senkou_a=(tenkan_sen+kijun_sen)/2
p1=plot(senkou_a, offset=displacement,color=color.blue)

senkou_b=donchian(senkou_b_len )
p2=plot(senkou_b,offset=26,color=color.maroon)
transp=70
fill(p1,p2,color=senkou_a>senkou_b?color.new(color.yellow, transp):color.new(color.red, transp))
var table ichimoku_table = table.new(position.top_right, columns=3, rows=22,bgcolor=color.yellow,border_width=1,border_color=color.white)
kumo_up_line=senkou_a[displacement]>=senkou_b[displacement]?senkou_a[displacement]:senkou_b[displacement]
kumo_down_line=senkou_a[displacement]>=senkou_b[displacement]?senkou_b[displacement]:senkou_a[displacement]
    
highest_senkou_a=highest(senkou_a[displacement],kumo_shadow_period)
lowest_senkou_a=lowest(senkou_a[displacement],kumo_shadow_period)
highest_senkou_b=highest(senkou_b[displacement],kumo_shadow_period)
lowest_senkou_b=lowest(senkou_b[displacement],kumo_shadow_period)
highest_cloud=highest_senkou_a>=highest_senkou_b?highest_senkou_a:highest_senkou_b
lowest_cloud=lowest_senkou_a<=lowest_senkou_b?lowest_senkou_a:lowest_senkou_b
slp = (change(kijun_sen)/kijun_sen) 
float score = 0.0
//price vs chikou span
kumo_up_line_chikou=senkou_a[displacement+displacement]>=senkou_b[displacement+displacement]?senkou_a[displacement+displacement]:senkou_b[displacement+displacement]
kumo_down_line_chikou=senkou_a[displacement+displacement]>=senkou_b[displacement+displacement]?senkou_b[displacement+displacement]:senkou_a[displacement+displacement]
chikou_high=0.0
chikou_low=low[displacement-1]
for i=displacement-1 to displacement-chikouspan_consolidation_bar
    chikou_high:=max(high[i],chikou_high)
    
for i=displacement-1 to displacement-chikouspan_consolidation_bar
    chikou_low:=min(low[i],chikou_low)
//KUMO future
senkou_a_slp=(change(senkou_a)/senkou_a) 
senkou_b_slp=(change(senkou_b)/senkou_b) 
tenkan_slp=(change(tenkan_sen)/tenkan_sen) 
tenkan_crossover=barssince(crossover(close,tenkan_sen) or crossunder(close,tenkan_sen))
kijun_sen_crossover=barssince(crossover(close,kijun_sen) or crossunder(close,kijun_sen))
ts_crossover=barssince(crossover(tenkan_sen,kijun_sen) or crossunder(tenkan_sen,kijun_sen))
kumo_crossover=barssince(crossover(close,kumo_up_line) or crossunder(close,kumo_down_line))
senkou_crossover=barssince(crossover(senkou_a,senkou_b) or crossunder(senkou_a,senkou_b))
tenkan_crossover_color=barssince(crossover(close,tenkan_sen))<barssince(crossunder(close,tenkan_sen))?color.green:color.red
kijun_sen_crossover_color=barssince(crossover(close,kijun_sen))<barssince(crossunder(close,kijun_sen))?color.green:color.red
ts_crossover_color=barssince(crossover(tenkan_sen,kijun_sen))<barssince(crossunder(tenkan_sen,kijun_sen))?color.green:color.red
kumo_crossover_color=barssince(crossover(close,kumo_up_line))<barssince(crossunder(close,kumo_down_line))?color.green:color.red
senkou_crossover_color=barssince(crossover(senkou_a,senkou_b))<barssince(crossunder(senkou_a,senkou_b))?color.green:color.red

if barstate.islast
    table.cell(ichimoku_table,0,0,'Ichimoku Trading')
    table.cell(ichimoku_table,1,0,'Date: '+tostring(dayofmonth(timenow))+'/'+tostring(month(timenow))+'/'+tostring(year(timenow)))
    if show_comment
        table.cell(ichimoku_table,2,0,'Comment')
    table.cell(ichimoku_table,0,1,"Price vs. Kumo")
    table.cell(ichimoku_table,0,2,"Kuma Shadows")
    table.cell(ichimoku_table,0,3,"Tenkan Sen/Kijun Sen")
    table.cell(ichimoku_table,0,4,"Are they in the Cloud?")
    table.cell(ichimoku_table,0,5,"Is Kijun Sen flat?")
    table.cell(ichimoku_table,0,6,"Price vs. Chikou Span")
    table.cell(ichimoku_table,0,7,'Is Chikou Span \nin Kumo Cloud?')
    table.cell(ichimoku_table,0,8,"if consolidates, will \nit run into price ("+tostring(chikouspan_consolidation_bar)+" bar)")
    table.cell(ichimoku_table,0,9,"Kumo Future ")
    table.cell(ichimoku_table,0,10,"Future Senkou A \nvs Future Senkou B")
    table.cell(ichimoku_table,0,11,"Price Vs. Tenkan Sen")
    table.cell(ichimoku_table,0,12,"Tenkan Sen Direction")
    table.cell(ichimoku_table,0,13,"Price Vs. Kinjen Sen")
    table.cell(ichimoku_table,0,14,"Price cross TS")
    table.cell(ichimoku_table,0,15,"Price cross KS")
    table.cell(ichimoku_table,0,16,"TS and KS Cross")
    table.cell(ichimoku_table,0,17,"Price cross Kumo")
    table.cell(ichimoku_table,0,18,"Future Kumo Twist")
    table.cell(ichimoku_table,0,19,"Overall Score")
    table.cell(ichimoku_table,0,20,"Distance of Price \nvs. Tenkan Sen?")
    table.cell(ichimoku_table,0,21,"Distance of Price \nvs. Kijun Sen?")

    
    //Price is above cloud
    if close>kumo_up_line
        table.cell(ichimoku_table,1,1,"↑ Bullish",text_color=color.green)
        if show_comment
            table.cell(ichimoku_table,2,1,'Price above Kumo and this indicates a bullish trend (+2).')
        score:=score + 2.0
        //kumo shadow behind price
        if close<highest_cloud
            table.cell(ichimoku_table,1,2,"Weak Bullish",text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,2,'Kumo shadow behind price, analyzing '+tostring(kumo_shadow_period)+' bars. Kumo shadow represents\n major resistances, hence weakens the bullish strength (-0.5)')
            score:=score - 0.5
        else
            table.cell(ichimoku_table,1,2,"Strong Bullish",text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,2,'No Kumo shadow behind price, analyzing '+tostring(kumo_shadow_period)+' bars. This strengthens the trend (+0.5)')
            score:=score + 0.5
            
    
    else if close<kumo_down_line
        table.cell(ichimoku_table,1,1,"↓ Bearish",text_color=color.red)
        if show_comment
            table.cell(ichimoku_table,2,1,'Price below Kumo, hence bearish(-2).')
        score:=score - 2.0
        //kumo shadow behind price
        if close>lowest_cloud
            table.cell(ichimoku_table,1,2,"Weak Bearish",text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,2,'Kumo shadow behind price, analyzing ' + tostring(kumo_shadow_period) + ' bars. Kumo shadow represents\n major supports, hence weakens the bearish strength (+0.5)')
            score:=score + 0.5
        else
            table.cell(ichimoku_table,1,2,"Strong Bearish",text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,2,'No Kumo shadow behind price analyzing ' + tostring(kumo_shadow_period) + ' bars. This strengthens the trend (-0.5)')
            score:=score - 0.5
    else
        table.cell(ichimoku_table,1,1,"Not Confirmed",text_color=color.gray)
        if show_comment
            table.cell(ichimoku_table,2,1,'Price within Kumo, so consolidating or no clear trend indication(0).')
        table.cell(ichimoku_table,1,2,"NA",text_color=color.red)
        if show_comment
            table.cell(ichimoku_table,2,2,'Price within Kumo')
        score:=score - 0
    
    //tenkan_sen vs. Kijun Sen
    if tenkan_sen>kijun_sen
        table.cell(ichimoku_table,1,3,"↑ Bullish",text_color=color.green)
        if show_comment
            table.cell(ichimoku_table,2,3,'Tenkan Sen above Kijun Sen. So, the short term trend is up, a bullish signal (+2)')
        score:=score + 2.0
        //if tenkan sen and kijun sen within kumo
        if tenkan_sen<=kumo_up_line and tenkan_sen>=kumo_down_line
            if kijun_sen<=kumo_up_line and kijun_sen>=kumo_down_line
                table.cell(ichimoku_table,1,4,"Tenkan Sen: Weak Bullish \n Kijun Sen: Weak Bullish",text_color=color.green)
                if show_comment
                    table.cell(ichimoku_table,2,4,'Tenkan Sen in cloud, hence reducing the trend strength (-0.5).\n Kijun Sen in cloud, hence reducing the trend strength (-0.5)')
                score:=score - 1.0
            else
                table.cell(ichimoku_table,1,4,"Tenkan Sen: Weak Bullish \n Kijun Sen: NA",text_color=color.green)
                if show_comment
                    table.cell(ichimoku_table,2,4,'Tenkan Sen in cloud, hence reducing the trend strength (-0.5).\n Kijun Sen not in cloud (0)')
                score:=score - 0.5
        else if kijun_sen<=kumo_up_line and kijun_sen>=kumo_down_line
            table.cell(ichimoku_table,1,4,"Tenkan Sen: NA \n Kijun Sen: Weak Bullish",text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,4,'Tenkan Sen not in cloud (0).\n Kijun Sen in cloud, hence reducing the trend strength (-0.5)')
            score:=score - 0.5 
        else
            table.cell(ichimoku_table,1,4,"Tenkan Sen: NA \n Kijun Sen: NA")
            if show_comment
                table.cell(ichimoku_table,2,4,'Tenkan Sen is not in cloud (0).\n Kijun Sen is not in cloud (0)')
            score:=score - 0
        if slp>0
            table.cell(ichimoku_table,1,5,"Strong Bullish",text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,5,'Kijun Sen is up, indicating a strong trend (0.5)')
            score:=score + 0.5
        else
            table.cell(ichimoku_table,1,5,"Weak Bullish",text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,5,'Kijun Sen is flat or down, indicating a weak trend (-0.5)')
            score:=score - 0.5
            
    else if tenkan_sen<kijun_sen
        table.cell(ichimoku_table,1,3,"↓ Bearish",text_color=color.red)
        if show_comment
            table.cell(ichimoku_table,2,3,'Tenkan Sen below Kijun Sen. So, the short term trend is down, a bearish signal (-2)')
        score:=score - 2.0
        if tenkan_sen<=kumo_up_line and tenkan_sen>=kumo_down_line
            if kijun_sen<=kumo_up_line and kijun_sen>=kumo_down_line
                table.cell(ichimoku_table,1,4,"Tenkan Sen: Weak Bearish \n Kijun Sen: Weak Bearish",text_color=color.red)
                if show_comment
                    table.cell(ichimoku_table,2,4,'Tenkan Sen in cloud, hence reducing the trend strength (+0.5).\n Kijun Sen in cloud, hence reducing the trend strength (+0.5)')
                score:=score + 1.0
            else
                table.cell(ichimoku_table,1,4,"Tenkan Sen: Weak Bearish \n Kijun Sen: NA",text_color=color.red)
                if show_comment
                    table.cell(ichimoku_table,2,4,'Tenkan Sen in cloud, hence reducing the trend strength (+0.5).\n Kijun Sen not in cloud (0)')
                score:=score + 0.5
        else if kijun_sen<=kumo_up_line and kijun_sen>=kumo_down_line
            table.cell(ichimoku_table,1,4,"Tenkan Sen: NA \n Kijun Sen: Weak Bearish",text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,4,'Tenkan Sen not in cloud (0).\n Kijun Sen in cloud, hence reducing the trend strength (+0.5)')
            score:=score + 0.5 
        else
            table.cell(ichimoku_table,1,4,"Tenkan Sen: NA \n Kijun Sen: NA")
            if show_comment
                table.cell(ichimoku_table,2,4,'Tenkan Sen is not in cloud (0).\n Kijun Sen is not in cloud (0)')
            score:=score - 0 
        if slp<0
            table.cell(ichimoku_table,1,5,"Strong Bearish",text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,5,'Kijun Sen is down, indicating a strong trend (-0.5)')
            score:=score - 0.5
        else
            table.cell(ichimoku_table,1,5,"Weak Bearish",text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,5,'Kijun Sen is flat or up, indicating a weak trend (-0.5)')
            score:=score + 0.5
    else
        table.cell(ichimoku_table,1,3,"Not Confirmed")
        table.cell(ichimoku_table,1,4,"NA")
        if show_comment
            table.cell(ichimoku_table,2,3,'Not Confirmed')
            table.cell(ichimoku_table,2,4,'Not Confirmed')
        score:=score + 0
        
        
    
    
    
    if close>close[26]
        table.cell(ichimoku_table,1,6,"Bullish",text_color=color.green)
        if show_comment
            table.cell(ichimoku_table,2,6,'Chikou span above price indicating a strong bullish move(0.5)')
        score:=score + 1
        if close<=kumo_up_line_chikou and close>=kumo_down_line_chikou
            table.cell(ichimoku_table,1,7,"Weak Bullish",text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,7,'Chikou Span in Kumo Cloud and weakening the strength of trend (-0.5)')
            score:=score - 0.5
        else
            table.cell(ichimoku_table,1,7,"NA",text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,7,'span not in Kumo cloud (0)')
            score:=score + 0
        if close<=chikou_high
            table.cell(ichimoku_table,1,8,'Price will \n run into price',text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,8,'If Chikou runs into the price, the trend strength will be lower. (-0.5)')
            score:=score - 0.5
        else
            pct_ch=((close-chikou_high)/close)*100
            // if pct_ch>2
            table.cell(ichimoku_table,1,8,'Price need to decline \n'+tostring(pct_ch,format.percent)+' to run into price',text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,8,'If Chikou is in open space the strength will be higher (+0.5)')
            score:=score + 0.5

        
                
            
                
    else
        table.cell(ichimoku_table,1,6,"Bearish",text_color=color.red)
        if show_comment
            table.cell(ichimoku_table,2,6,'Chikou span below price indicating a strong bearish move(-0.5)')
        score:=score + 1
        if close<=kumo_up_line_chikou and close>=kumo_down_line_chikou
            table.cell(ichimoku_table,1,7,"Weak Bearish",text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,7,'Chikou Span is in Kumo Cloud and weakening the strength of trend (+0.5)')
            score:=score + 0.5
        else
            table.cell(ichimoku_table,1,7,"NA",text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,7,'Chikou span not in Kumo cloud (0)')
            score:=score - 0.0
        if close>=chikou_low
            table.cell(ichimoku_table,1,8,'Price will \n run into price',text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,8,'If Chikou runs into the price, the trend strength will be lower. (+0.5)')
            score:=score + 0.5
        else
            pct_ch=((chikou_low-close)/close)*100
            //if pct_ch>2
            table.cell(ichimoku_table,1,8,'Price need to raise \n'+tostring(pct_ch,format.percent)+' to run into price',text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,8,'If Chikou is in open space the strength will be higher (-0.5)')
            score:=score - 0.5
            // else
            //     table.cell(ichimoku_table,1,8,'Price need to raise \n'+tostring(pct_ch,format.percent)+' to run into price',text_color=color.red)
            //     score:=score + 0.0
    
    
    if senkou_a>senkou_b
        table.cell(ichimoku_table,1,9,'Bullish',text_color=color.green)
        if show_comment
            table.cell(ichimoku_table,2,9,'Senkou A above Senkou B, so Kumo future is Bullish (+0.5)')
        score:=score+1
        if senkou_a_slp>0 and senkou_b_slp==0
            table.cell(ichimoku_table,1,10,'Bullish',text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,10,'Senkou A is pointing up and Senkou B is flat, hence Bullish (+0.5)')
            score:=score+.5
        else if senkou_a_slp<0 and senkou_b_slp==0
            table.cell(ichimoku_table,1,10,'Weak Bullish',text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,10,'Senkou A is pointing down and Senkou B is flat, hence Weaken the trend (-0.5)')
            score:=score-.5
        else if senkou_a_slp>0 and senkou_b_slp>0
            table.cell(ichimoku_table,1,10,'Bullish',text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,10,'Both Senkou A and B are pointing UP, hence Bullish (+0.5)')
            score:=score+.5
        else if senkou_a_slp<0 and senkou_b_slp>0
            table.cell(ichimoku_table,1,10,'Weak Bullish',text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,10,'Senkou A is pointing down and Senkou B is up, hence Weaken the trend (-0.5)')
            score:=score-.5
        else
            table.cell(ichimoku_table,1,10,'NA',text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,10,'Senkou A and B is not confirmed(0)')
            score:=score-0
        
        
    else if senkou_a<senkou_b
        table.cell(ichimoku_table,1,9,'Bearish',text_color=color.red)
        if show_comment
            table.cell(ichimoku_table,2,9,'Senkou A below Senkou B, so Kumo future is Bearish (-0.5)')
        score:=score-1
        if senkou_a_slp<0 and senkou_b_slp==0
            table.cell(ichimoku_table,1,10,'Bearish',text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,10,'Senkou A is pointing down and Senkou B is flat, hence Bearish (-0.5)')
            score:=score-.5
        else if senkou_a_slp>0 and senkou_b_slp==0
            table.cell(ichimoku_table,1,10,'Weak Bearish',text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,10,'Senkou A is pointing up and Senkou B is flat, hence Weaken the trend (+0.5)')
            score:=score+.5
        else if senkou_a_slp<0 and senkou_b_slp<0
            table.cell(ichimoku_table,1,10,'Bearish',text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,10,'Both Senkou A and B are pointing down, hence Bearish (-0.5)')
            score:=score-.5
        else if senkou_a_slp>0 and senkou_b_slp<0
            table.cell(ichimoku_table,1,10,'Weak Bearish',text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,10,'Senkou A is pointing up and Senkou B is down, hence Weaken the trend (+0.5)')
            score:=score+.5
        else
            table.cell(ichimoku_table,1,10,'NA',text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,10,'Senkou A and B is not confirmed(0)')
            score:=score-0
    else
        table.cell(ichimoku_table,1,9,'NA',text_color=color.red)
        if show_comment
            table.cell(ichimoku_table,2,9,'Kumo future position is not confirmed(0)')
        score:=score-0
        
    
    
    if close>tenkan_sen
        table.cell(ichimoku_table,1,11,'Bullish',text_color=color.green)
        if show_comment
            table.cell(ichimoku_table,2,11,'Close above Tenkan Sen,hence Bullish(+.5)')
        score:=score+.5
        if tenkan_slp>0
            table.cell(ichimoku_table,1,12,'Strong Bullish',text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,12,'Tenkan Sen is pointing up, a Bullish indication(+0.5)')
            score:=score+.5
        else
            table.cell(ichimoku_table,1,12,'Weak Bullish',text_color=color.green)
            if show_comment
                table.cell(ichimoku_table,2,12,'Tenkan Sen is flat or down,hence trend strength is weak(-0.5)')
            score:=score-.5
            
        
    else
        table.cell(ichimoku_table,1,11,'Bearish',text_color=color.red)
        if show_comment
            table.cell(ichimoku_table,2,11,'Close below Tenkan Sen,hence Bearish(-0.5)')
        score:=score-.5
        if tenkan_slp<0
            table.cell(ichimoku_table,1,12,'Strong Bearish',text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,12,'Tenkan Sen is pointing down, a Bearish indication(-0.5)')
            score:=score-.5
        else
            table.cell(ichimoku_table,1,12,'Weak Bearish',text_color=color.red)
            if show_comment
                table.cell(ichimoku_table,2,12,'Tenkan Sen is flat or up,hence trend strength is weak(+0.5)')
            score:=score+.5
    
    if close>kijun_sen
        table.cell(ichimoku_table,1,13,'Bullish',text_color=color.green)
        if show_comment
            table.cell(ichimoku_table,2,13,'Close above Kijun Sen,hence Bullish(+.5)')
        score:=score+.5
    else
        table.cell(ichimoku_table,1,13,'Bearish',text_color=color.red)
        if show_comment
            table.cell(ichimoku_table,2,13,'Close below Kijun Sen,hence Bullish(-.5)')
        
        score:=score-.5
        
    table.cell(ichimoku_table,1,14,tostring(tenkan_crossover)+' bars ago',text_color=tenkan_crossover_color)
    if show_comment
        table.cell(ichimoku_table,2,14,'Price, Tenkan Sen crossover considered Bullish\nand crossunder considered Bearish',text_color=tenkan_crossover_color) 
    table.cell(ichimoku_table,1,15,tostring(kijun_sen_crossover)+' bars ago',text_color=kijun_sen_crossover_color)   
    if show_comment
        table.cell(ichimoku_table,2,15,'Price, Kijun Sen crossover considered Bullish\nand crossunder considered Bearish',text_color=tenkan_crossover_color)
    table.cell(ichimoku_table,1,16,tostring(ts_crossover)+' bars ago',text_color=ts_crossover_color)  
    if show_comment
        table.cell(ichimoku_table,2,16,'Bullish if TS crossover KS, Bearish if crossunder',text_color=tenkan_crossover_color)
    table.cell(ichimoku_table,1,17,tostring(kumo_crossover)+' bars ago',text_color=kumo_crossover_color)
    if show_comment
        table.cell(ichimoku_table,2,17,'Bullish if Price cross above Kumo, Bearish if Price cross below Kumo',text_color=tenkan_crossover_color)
    table.cell(ichimoku_table,1,18,tostring(senkou_crossover)+' bars ago',text_color=senkou_crossover_color)
    if show_comment
        table.cell(ichimoku_table,2,18,'Bullish if Senkou A crosses B, Bearish if  Senkou B crosses A',text_color=tenkan_crossover_color)
    
    
    table.cell(ichimoku_table,1,19,tostring(score),text_color=score<=0?color.red:color.green)   
    if show_comment
        table.cell(ichimoku_table,2,19,'Cumulative strength of all indicators',text_color=tenkan_crossover_color)
        
    table.cell(ichimoku_table,1,20,tostring(abs(close-tenkan_sen)))
    if show_comment
        table.cell(ichimoku_table,2,20,'If price goes far away from Tenkan Sen it may come down to match TS')
    table.cell(ichimoku_table,1,21,tostring(abs(close-kijun_sen))) 
    if show_comment
        table.cell(ichimoku_table,2,21,'If price goes far away from Kijun Sen it may come down to match KS')
